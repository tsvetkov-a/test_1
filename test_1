function getorderstage(o_id in number) return varchar2 is
  /* Возвращает стадию обработки заказа */
  /* 04.06.2008 Трунов - исправлен статус "осмечен" ('02')*/
  /* 19.07.2011 Трунов - добавлен статус "Тех. отчет" ('10')*/
  /* 11.04.2012 Трунов - исправлен статус "закрыт" ('07') - все этапы аанулированы после отгрузки (Плоткина)*/
  /* 12.09.2016 Трунов - исправлен статус "Тех. отчет" ('10') - добавлена работа "Изготовление копий ТО" (kind = 926) */
  /* 24.04.2019 Трунов - исправлен статус "Просрочен","Тех. отчет" ('06','10') - добавлена работа "Составление программы изысканий" (kind = 785) */

  -- Курсор для получения информации о заказе
  cursor c_order(o_id number) is
    select id, no, open_date, duty_date, cancel_date, ent_id, done_doc_date, done_doc_n, kind, creator, e_date, done_doc_type, arb_id
    from ppo_orders o left join (select ord_id, max(id) arb_id from ppo_delay where delay = '1' and s_date is not null and e_date is null group by ord_id) a on a.ord_id = o.id
    where id = o_id;

  -- Курсор для получения максимальной даты окончания этапов заказа
  cursor c_ed (o_id in number) is
    select max(f.e_date) from (
      select nvl(s.accel_per, s.e_date) e_date from ppo_voldates s where s.ord_id = o_id
      union
      select nvl(s.accel_per, s.e_date) e_date from ppo_voldates w, ppo_voldates s where w.ord_id = o_id and s.voldate_id = w.id
    ) f;

  -- Объявление переменных
  current_day constant date := trunc(sysdate); -- Текущая дата
  stage varchar2(2) := '00'; -- Стадия заказа
  x1 pls_integer := 0; -- Вспомогательная переменная 1
  x2 pls_integer := 0; -- Вспомогательная переменная 2
  max_ed date; -- Максимальная дата окончания этапов заказа
  r_order c_order%rowtype; -- Строка результата курсора c_order
  v number; -- Сумма значений value и value_fact для всех этапов заказа
  cnt number; -- Количество незакрытых этапов заказа
  cnt_stg number; -- Количество этапов заказа
  cnt_wrk number; -- Количество незакрытых работ заказа

begin
    -- Если заказ не указан, возвращаем статус "новый"
  if o_id is null then return '00'; end if;

  -- Получаем информацию о заказе
  open c_order(o_id);
  fetch c_order into r_order;
  close c_order;

  -- Проверка арбитража: если есть арбитраж, возвращаем статус '09'
  if r_order.arb_id is not null then return '09'; end if;

  -- Если заказ аннулирован, возвращаем статус '08'
  if r_order.cancel_date is not null then return '08'; end if;

  -- Если заказ закрыт, возвращаем статус '07'
  if (r_order.done_doc_date is not null and r_order.done_doc_n is not null)
  or r_order.done_doc_type = '5' /* заказ закрыт реестром */
  then
    return '07';
  end if;

  -- Получаем сумму значений value и value_fact для всех этапов заказа
  select sum(w.value) + sum(nvl(w.value_fact,0)) into v from ppo_voldates w where w.ord_id = o_id;

  -- Определение стадии заказа
  if r_order.ent_id is not null then
    stage := '01';
    -- Осмечен, если деньги в работе/этапе > 0
    if v > 0 then stage := '02'; end if;
    if r_order.duty_date is not null then stage := '03'; end if;
  end if;

  -- Проверяем выполнение этапов заказа
  if v is not null then
    -- Проверяем, все ли этапы имеют дату фактического окончания
    select count(w.id) into x1 from ppo_voldates w where w.ord_id = o_id and w.is_done != 'Y';

    -- Если все этапы выполнены, стадия заказа - '05'
    if x1 = 0 then
      stage := '05';
    else
      -- Проверяем, все ли этапы имеют дату фактического окончания, кроме работ "Составление технического отчета", "Составление программы изысканий"
      select count(w.id) into x1 from ppo_voldates w where w.ord_id = o_id and w.is_done != 'Y' and nvl(kind,'0') not in ('371','926','785');
      if x1 > 0 then
        select count(w.e_date_fact) into x2 from ppo_voldates w where w.ord_id = o_id and nvl(kind,'0') not in ('371','926','785');
        -- Получаем максимальную дату окончания этапов всего заказа кроме работ "Составление технического отчета", "Составление программы изысканий"
        -- и считаем это датой окончания заказа
        open c_ed(o_id); fetch c_ed into max_ed; close c_ed;
                if max_ed >= current_day then
          -- Если максимальная дата окончания этапов еще не наступила, стадия заказа - '04'
          stage := '04';
        else
          -- Если максимальная дата окончания этапов прошла, стадия заказа - '06' (просрочен)
          stage := '06';
        end if;
      else
        -- Если все этапы, кроме работ "Составление технического отчета", "Составление программы изысканий" выполнены, стадия заказа - '10' (тех. отчет)
        stage := '10';
      end if;
    end if;
  end if;

  return stage;
end getorderstage;



wsl : Имя "wsl" не распознано как имя командлета, функции, файла сценария или выполняемой программы. Проверьте правильн
ость написания имени, а также наличие и правильность пути, после чего повторите попытку.
строка:1 знак:1
+ wsl --install
+ ~~~
    + CategoryInfo          : ObjectNotFound: (wsl:String) [], CommandNotFoundException
    + FullyQualifiedErrorId : CommandNotFoundException
    
